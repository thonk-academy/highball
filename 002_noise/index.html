<!DOCTYPE html>
<html>
  <head>
    <style>
      body { margin: 0; }
      canvas { width: 100%; height: 100%; }
      .surface { width: 100vh; height: 100vh; }
    </style>
  </head>
  <body>
    <div class="surface" id="container"/>
    <script src="../js/three.js"></script>
    <script id="vertexShader" type="x-shader/x-vertex">
      varying vec2 vUv;
      uniform vec2 resolution;
      void main()  {
        vUv = uv;
        float aspect = resolution.x / resolution.y;
        vUv.x = ((vUv.x * 2.0 - 1.0) * aspect) * 0.5 + 0.5;
        gl_Position = vec4( position, 1.0 );
      }
    </script>

    <script id="fragmentShader" type="x-shader/x-fragment">
      varying vec2 vUv;
      uniform float time;

      vec4 mod289(vec4 x)
      {
        return x - floor(x * (1.0 / 289.0)) * 289.0;
      }

      vec4 permute(vec4 x)
      {
        return mod289(((x*34.0)+1.0)*x);
      }

      vec4 taylorInvSqrt(vec4 r)
      {
        return 1.79284291400159 - 0.85373472095314 * r;
      }

      vec2 fade(vec2 t) {
        return t*t*t*(t*(t*6.0-15.0)+10.0);
      }

      // Classic Perlin noise, ripped from https://github.com/stegu/webgl-noise
      float cnoise(vec2 P)
      {
        vec4 Pi = floor(P.xyxy) + vec4(0.0, 0.0, 1.0, 1.0);
        vec4 Pf = fract(P.xyxy) - vec4(0.0, 0.0, 1.0, 1.0);
        Pi = mod289(Pi); // To avoid truncation effects in permutation
        vec4 ix = Pi.xzxz;
        vec4 iy = Pi.yyww;
        vec4 fx = Pf.xzxz;
        vec4 fy = Pf.yyww;

        vec4 i = permute(permute(ix) + iy);

        vec4 gx = fract(i * (1.0 / 41.0)) * 2.0 - 1.0 ;
        vec4 gy = abs(gx) - 0.5 ;
        vec4 tx = floor(gx + 0.5);
        gx = gx - tx;

        vec2 g00 = vec2(gx.x,gy.x);
        vec2 g10 = vec2(gx.y,gy.y);
        vec2 g01 = vec2(gx.z,gy.z);
        vec2 g11 = vec2(gx.w,gy.w);

        vec4 norm = taylorInvSqrt(vec4(dot(g00, g00), dot(g01, g01), dot(g10, g10), dot(g11, g11)));
        g00 *= norm.x;
        g01 *= norm.y;
        g10 *= norm.z;
        g11 *= norm.w;

        float n00 = dot(g00, vec2(fx.x, fy.x));
        float n10 = dot(g10, vec2(fx.y, fy.y));
        float n01 = dot(g01, vec2(fx.z, fy.z));
        float n11 = dot(g11, vec2(fx.w, fy.w));

        vec2 fade_xy = fade(Pf.xy);
        vec2 n_x = mix(vec2(n00, n01), vec2(n10, n11), fade_xy.x);
        float n_xy = mix(n_x.x, n_x.y, fade_xy.y);
        return 2.3 * n_xy;
      }

      void main()  {
        float slow = cnoise(vec2(time * 0.2, 0.0));
        float fast = 0.2 * cnoise(12.0 * vec2(0.0, time * 0.2));
        float rlow = 0.15;
        float rhigh = 0.5;
        float range = rhigh - rlow;
        float period = 10.0;
        float pstart = period / 2.0 - period / 10.0;
        float pend = period / 2.0 + period / 10.0;
        float tmod = mod(time, period);
        float weight = smoothstep(pstart, period / 2.0, tmod) - smoothstep(period / 2.0, pend, tmod);
        float pulse = mix(slow, fast, weight);
        float r = (pulse + 1.0) * (range / 2.0) + rlow;
        float r2 = r * r;
        vec2 center = vec2(0.5, 0.5);
        vec2 pos = center - vUv;
        float warp0 = 0.08 * cnoise(5.0 * vUv + vec2(time * 0.2, 0.0));
        float warp1 = 0.08 * cnoise(5.0 * vUv + vec2(0.0, time * 0.2));
        pos.x += warp0;
        pos.y += warp1;
        // float n = step(r2, dot(pos, pos));
        // float n = smoothstep(0.0, r2, dot(pos, pos));
        float n = smoothstep(r2 - 0.04, r2, dot(pos, pos));
        float n2 = cnoise(9.0 * (vUv + 0.5 * vec2(warp0, warp1) + vec2(time * 0.0, 0.0))) * 0.5 + 0.5;
        n2 = step(0.4, n2);
        n *= n2;
        vec3 color0 = vec3(0.0, 0.0, 0.0);
        vec3 color1 = vec3(1.0, 0.0, 0.0);
        vec3 colorOut = mix(color0, color1, n);
        gl_FragColor = vec4(colorOut, 1.0);
      }
    </script>
    <script>
      let container = document.getElementById( 'container' );
      var scene = new THREE.Scene();
      var camera = new THREE.OrthographicCamera(-1, 1, 1, -1, 0, 1);

      var renderer = new THREE.WebGLRenderer();
      renderer.setPixelRatio( window.devicePixelRatio );
      container.appendChild( renderer.domElement );

      let onWindowResize = function (event) {
        // renderer.setSize( window.innerWidth, window.innerHeight );
        renderer.setSize( container.offsetWidth, container.offsetHeight );
        uniforms.resolution.value = new THREE.Vector2(  container.offsetWidth, container.offsetHeight );
      }

      window.addEventListener('resize', onWindowResize, false);

      var geometry = new THREE.PlaneBufferGeometry(2, 2);
      let uniforms = {
        time: { value: 1.0 },
        resolution: { type: "v2", value: new THREE.Vector2( window.innerWidth, window.innerHeight ) },
      };
      var material = new THREE.ShaderMaterial( {
        uniforms: uniforms,
        vertexShader: document.getElementById( 'vertexShader' ).textContent,
        fragmentShader: document.getElementById( 'fragmentShader' ).textContent
      } );
      var surface = new THREE.Mesh( geometry, material );
      scene.add( surface );

      onWindowResize();

      var animate = function (timestamp) {
        requestAnimationFrame( animate );

        uniforms.time.value = timestamp / 1000;

        renderer.render( scene, camera );
        };

      animate();
    </script>
  </body>
</html>
